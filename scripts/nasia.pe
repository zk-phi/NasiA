#!/usr/bin/fontforge -script

halfwidth = 600
fullwidth = 1200

# ---- Parse command line args

iawriter = ""
nasum    = ""
output   = ""

use_visiblespace = "no"

i = 1
while (i < $argc)
  if ($argv[i] == "--visiblespace")
    use_visiblespace = "yes"
  elseif (iawriter == "")
    iawriter = $argv[i]
  elseif (nasum == "")
    nasum = $argv[i]
  elseif (output == "")
    output = $argv[i]
  endif
  i = i + 1
endloop

# ---- Merge undefined glyphs from NasuM into iAWriter

Open(iawriter)
Print("nasia: Merging ...")
MergeFonts(nasum)

# ---- Zenkaku space

if (use_visiblespace == "yes")
  Print("nasia: Creating zenkaku space glyph ...")

  # ballot box (☐)
  Select(0u2610)
  Copy()
  Select(0u3000)
  Paste()

  # heavy greek cross (✚)
  Select(0u271a)
  Copy()
  Select(0u3000)
  PasteInto()

  OverlapIntersect()

  RoundToInt()
  RemoveOverlap()
  RoundToInt()
endif

# ---- Remove hints

Print("nasia: Removing old hints ...")
SelectWorthOutputting()
ClearHints()
DontAutoHint()

# ---- Font generation

Print("nasia: Generating the font ...")
SetFontNames("nasia", "nasia", "nasia")
SetUniqueID(0)
Generate(output)

Close()
